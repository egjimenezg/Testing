apply plugin: 'groovy'

repositories{ 
  jcenter() 
}

//Drivers de los navegadores
ext.drivers = ['firefox','chrome']

dependencies{ 
  compile 'org.codehaus.groovy:groovy-all:2.3.6'

  def gebVersion = '0.9.3'
  def seleniumVersion = '2.42.2'

  //Si se utiliza Spock, es necesaria la dependencia de geb-spock 
  testCompile "org.gebish:geb-spock:$gebVersion"
  testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
  
  testCompile "org.seleniumhq.selenium:selenium-support:${seleniumVersion}"
  
  //Si se utiliza JUnit, es necesaria la dependencia de geb-junit (3 o 4)
  testCompile "org.gebish:geb-junit4:$gebVersion" 

  drivers.each{ driver ->
    testCompile "org.seleniumhq.selenium:selenium-${driver}-driver:${seleniumVersion}"
  }
}

drivers.each{ driver ->
  task "${driver}Test"(type:Test){
    reports{
      html.destination = reporting.file("$name/tests")
      junitXml.destination = file("$buildDir/test-results/$name")      
    }
    
    systemProperty  "geb.build.reportsDir",reporting.file("$name/geb")
    systemProperty  "geb.env",driver
    systemProperty  "webdriver.chrome.driver","${System.properties["user.home"]}/.grails/chromedriver"
    systemProperty  "geb.build.baseUrl","http://localhost:8080/Converter"
  }
}

test  {
  dependsOn drivers.collect { tasks["${it}Test"]  } 
  enabled = false
}

task wrapper(type: Wrapper){
  gradleVersion = '2.0'
}
